;=====================================================================
; Scroll messages up the screen
;---------------------------------------------------------------------
; Requires 8k+ memory
;---------------------------------------------------------------------
; Copyright (C) 2021 Lawrence Woodman <lwoodman@vlifesystems.com>
; Licensed under an MIT licence.  Please see LICENCE.md for details.
;=====================================================================

FDX         = $CB             ; Matrix key coordinate of key press

            .byt  $00, $20    ; Load address ($2000 / 8192)

            * = $2000
            jmp  main
#include "sideprint.a65"

main        .(
            jsr  SP_init

forever     lda  #<msg
            sta  STRADDR
            lda  #>msg
            sta  STRADDR+1
            jsr  scrollText
            jmp  forever
.)

delay       .(
getKey      lda  FDX
            cmp  #64          ; No key pressed
            bne  getKey       ; Wait if key pressed

            ; Wait for a little bit
            ldx  #$10
loop1       ldy  #$FF
loop2       dey
            bne  loop2
            dex
            bne  loop1
            rts
.)


scrollText  .(
loop        ldy  #00
            lda  (STRADDR), y
            beq  done
            jsr  putch
            lda  CRX
            bne  notFirstCol  ; If in first column
            jsr  delay
notFirstCol inc  STRADDR      ; Increment STRADDR
            bne  incDone      ; |
            inc  STRADDR+1    ; /
incDone
            jmp  loop
done        rts
.)

msg         .aasc "64 columns (sideways) on the Commodore VIC-20!",NLCHR
            .aasc NLCHR
            .aasc "Supports Newline (software definable) and ASCII 32-95",NLCHR
            .aasc NLCHR
            .aasc "Fonts are 4*8 with glyph using 3*7",NLCHR
            .aasc NLCHR
            .aasc "Also supports text modes more suitable for NTSC:",NLCHR
            .aasc "  56*16 and 52*16",NLCHR
            .aasc NLCHR
            .aasc "The alphabet:",NLCHR
            .aasc "  abcdefghijklmnopqrstuvwxyz",NLCHR
            .aasc "The numbers:",NLCHR
            .aasc "  0123456789",NLCHR
            .aasc "The rest:",NLCHR
            .aasc "  !",34,"#$%&'()*+,-./:;<=>?@[\]^_",NLCHR
            .aasc  NLCHR
            .aasc "Where there's a will there's a way!",NLCHR
            .aasc  NLCHR,0
